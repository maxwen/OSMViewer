import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'application'
    id 'cpp'
    id 'org.jetbrains.kotlin.jvm' version '1.8.10'
}

sourceCompatibility = '13'
targetCompatibility = '13'

group 'com.maxwen'
version 'unspecified'

application {
    mainClass = 'com.maxwen.osmviewer.routing.Main'
    applicationDefaultJvmArgs = ["-Dosm.db.path=${System.properties['user.home']}/Maps/osm/db2",
                                 "-Djava.library.path=" + file("${buildDir}/libs/routing/shared").absolutePath,
                                 "-Xmx8192m" ]
}

sourceSets.main {
    java {
        srcDir 'src/main/java'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":shared")
    testImplementation 'junit:junit:4.+'
    implementation 'com.github.cliftonlabs:json-simple:3.1.1'
    implementation 'org.xerial:sqlite-jdbc:3.34.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

model {
    platforms {
        x64 {
            architecture "x86_64"
        }
    }

    components {
        routing(NativeLibrarySpec) {
            targetPlatform "x64"
            binaries.all {
                def jvmHome = Jvm.current().javaHome
                if (targetPlatform.operatingSystem.linux) {
                    cppCompiler.args '-I', "${jvmHome}/include"
                    cppCompiler.args '-I', "${jvmHome}/include/linux"
                    cppCompiler.args '-D_FILE_OFFSET_BITS=64'
                    linker.args '-L', "/home/maxl/Downloads/pgrouting/build/lib/"
                    linker.args '-l', "pgrouting-3.4"
                    linker.args '-l', "sqlite3"
                }
            }
        }
    }
}

classes.dependsOn 'routingSharedLibrary'

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}