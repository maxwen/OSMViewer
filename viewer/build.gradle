import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'application'
    id 'cpp'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

javafx {
    version = '17.0.2'
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

sourceCompatibility = '13'
targetCompatibility = '13'

group 'com.maxwen'
version '1.0-SNAPSHOT'

application {
    mainClass = 'com.maxwen.osmviewer.Main'
    applicationDefaultJvmArgs = ["-Dosm.db.path=${System.properties['user.home']}/Maps/osm/db",
                                 "-Dosm.tiles.path=${System.properties['user.home']}/Maps/osm/tiles",
                                 "-Djava.library.path=" + file("${buildDir}/libs/routing/shared").absolutePath,
                                 "-Xmx8192m", "-Dprism.verbose=true", "-Dprism.forceGPU=true" ]
}

sourceSets.main {
    java {
        srcDir 'src/main/java'
    }
    resources {
        srcDirs = ['src/main/resources']
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":shared")
    testImplementation 'junit:junit:4.+'
    implementation 'com.github.cliftonlabs:json-simple:3.1.1'
    implementation 'org.xerial:sqlite-jdbc:3.34.0'
    implementation 'com.fazecast:jSerialComm:2.6.0'
    implementation('org.controlsfx:controlsfx:11.1.1')
}

model {
    platforms {
        x64 {
            architecture "x86_64"
        }
    }

    components {
        routing(NativeLibrarySpec) {
            targetPlatform "x64"
            binaries.all {
                def jvmHome = Jvm.current().javaHome
                if (targetPlatform.operatingSystem.linux) {
                    cppCompiler.args '-I', "${jvmHome}/include"
                    cppCompiler.args '-I', "${jvmHome}/include/linux"
                    cppCompiler.args '-D_FILE_OFFSET_BITS=64'
                    linker.args '-L', "/home/maxl/Downloads/pgrouting/build/lib/"
                    linker.args '-l', "pgrouting-3.4"
                    linker.args '-l', "sqlite3"
                }
            }
        }
    }
}

classes.dependsOn 'routingSharedLibrary'